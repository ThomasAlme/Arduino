#include <SoftwareSerial.h>
#include <WiFiEsp.h>
#include <Servo.h>

SoftwareSerial wifi_modul(6, 7);
WiFiEspServer server(8090);

const char ssid[] = "SkoleNett";
const char passord[] = "SkoleNett123";

const int LED = 10;
const int LED_OTHER = 11;
const int SERVO_PIN = 5; // PWM

Servo servo;

// Brukervennlig hjørne

void enablePins() {
  pinMode(LED, OUTPUT);
  pinMode(LED_OTHER, OUTPUT);
  servo.attach(SERVO_PIN);
}


// Denne funksjonen kjører når serveren får melding
void messageReceived(WiFiEspClient client, String ip, String melding) {
  String kommando1 = split(melding, ' ', 0);
  String kommando2 = split(melding, ' ', 1);

  Serial.print("Kommando1: ");
  Serial.println(kommando1);

  Serial.print("Kommando2: ");
  Serial.println(kommando2);
  
  int MY_LED = 0;

  if (ip == "192.168.1.200") {
    MY_LED = LED;
  } else {
    MY_LED = LED_OTHER;
  }
  
  if (kommando1 == "lys_på") {
      digitalWrite(MY_LED, HIGH);

  } else if (kommando1 == "lys_av") {
      digitalWrite(MY_LED, LOW);
      
  } else if (kommando1 == "lys_toggle") {
      digitalWrite(MY_LED, !digitalRead(MY_LED));
  } 

  if (kommando1 == "servo") {
    int grader = kommando2.toInt();
    servo.write(grader);
  }
}

// Selve programmet

int unstuck = 0;
int wifi_status = WL_IDLE_STATUS;

// Bruk denne funksjonen til å sende melding. Ikke bruk '\r' i meldingene.
void sendMessage(WiFiEspClient client, String melding) {
  client.print(melding);
  client.print("\r");
  delay(20);
}

String getMessage(WiFiEspClient client) {
  String melding = "";
  unstuck = 0;
  
  while (client.connected()) {
    unstuck++;
    if (unstuck > 30) {
        break;
      }
    if (client.available()) {
      unstuck--;
      char c = client.read();
      if (c == '\r') {
        Serial.print("Fikk melding: ");
        Serial.println(melding);
        break;
      } else {
        melding += c;
      }
    }
  }

  return melding;
}


void setup() {
  Serial.begin(115200);
  Serial.println("Starter WiFiRemote server...");
  
  wifi_modul.begin(9600);

  enablePins();
  
  // WiFi starter opp
  WiFi.init(&wifi_modul);

  if (WiFi.status() == WL_NO_SHIELD) {
    Serial.println("WiFi modul ikke tilkoblet");
    while(true); // Stopp programmet.
  }

  while (wifi_status != WL_CONNECTED) {
    Serial.print("Prøver å koble til trådløst nettverk: ");
    Serial.println(ssid);

    wifi_status = WiFi.begin(ssid, passord);
  }

  Serial.println("Koblet til nettverket!");
  printWiFiStatus();

  server.begin();
  Serial.println("WiFiRemote server er operativ!\n");
}

void loop() {
  WiFiEspClient client = server.available();

  if (client) {
    Serial.print("Ny tilkobling fra '");
    Serial.print(client.remoteIP());
    Serial.println("'!");

    String melding = getMessage(client);
    messageReceived(client, ipToString(client.remoteIP()), melding);
    
    delay(10);
    sendMessage(client, melding);

    /*
     
    melding = getMessage(client);

    if (melding != "OK") {
      delay(1000);
    }

    */
    
    client.stop();
    Serial.println("Klient koblet fra\n");
  }
}

void printWiFiStatus() {
  Serial.print("SSID: ");
  Serial.println(WiFi.SSID());

  IPAddress ip = WiFi.localIP();
  Serial.print("IP Adresse: ");
  Serial.println(ip);

  long rssi = WiFi.RSSI();
  Serial.print("Signal styrke (RSSI): ");
  Serial.print(rssi);
  Serial.println("dBm");
}

String ipToString(IPAddress ip) {
  String s = "";
  for (int i = 0; i < 4; i++) {
    s += i ? "." + String(ip[i]) : String(ip[i]);
  }
  return s;
}

// https://stackoverflow.com/questions/9072320/split-string-into-string-array
String split(String data, char separator, int index)
{
  int found = 0;
  int strIndex[] = {0, -1};
  int maxIndex = data.length()-1;

  for(int i=0; i<=maxIndex && found<=index; i++){
    if(data.charAt(i)==separator || i==maxIndex){
        found++;
        strIndex[0] = strIndex[1]+1;
        strIndex[1] = (i == maxIndex) ? i+1 : i;
    }
  }

  return found>index ? data.substring(strIndex[0], strIndex[1]) : "";
}
